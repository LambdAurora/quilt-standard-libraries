plugins {
	id("qsl.module")
}

qslModule {
	name = "Quilt DataFixerUpper API"
	moduleName = "datafixerupper"
	id = "quilt_datafixerupper"
	description = "Provides APIs for adding custom DataFixers."
	library = "misc"
	moduleDependencies {
		core {
			impl("lifecycle_events")
		}
	}
	entrypoints {
		events {
			values = ["org.quiltmc.qsl.datafixerupper.impl.Initializer"]
		}
		client_events {
			values = ["org.quiltmc.qsl.datafixerupper.impl.client.ClientInitializer"]
		}
	}
}

sourceSets {
	testmodV2 {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

dependencies {
	testmodV2Implementation sourceSets.main.output
}

task prepareDfuTestmod {
	doLast {
		delete "run"
		mkdir "run"
		logger.quiet("NOTE: By running this testmod, you automatically agree to the EULA.")
		file("run/eula.txt").text = "true"
	}
}

loom {
	runs {
		autoTestServerDfuTestmodV2 {
			server()
			configName = "Auto test server - DFU testmod v2"
			source(project.sourceSets.testmodV2)
			property("quilt.auto_test")
			programArg("--nogui")
		}
	}
}

rootProject.afterEvaluate {
	rootProject.tasks.runAutoTestServer.dependsOn prepareDfuTestmod
    rootProject.tasks.runAutoTestServer.finalizedBy runAutoTestServerDfuTestmodV2
}
